buildscript {
    ext {
        spring_boot_version = '1.5.8.RELEASE'
        elasticache_client_version = '1.1.1'
        commons_logging_version = '1.2'
    }
    repositories {
        maven { url 'http://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$spring_boot_version")
        classpath("io.spring.gradle:propdeps-plugin:0.0.9.RELEASE")
        classpath("com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3")
        classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.1")
    }
}

ext.JAVA_GRADLE = "$rootDir/gradle/java.gradle"
ext.RELEASE_GRADE = "$rootDir/gradle/release.gradle"

subprojects {
    apply plugin: 'org.springframework.boot'
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-maven'
    apply plugin: 'propdeps-idea'
    apply plugin: 'propdeps-eclipse'
    apply plugin: 'org.sonarqube'
    apply plugin: 'jacoco'
    apply from: JAVA_GRADLE
    apply from: RELEASE_GRADE

    def gitVersion = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            ignoreExitValue true
            commandLine 'git', 'describe', '--tags', '--abbrev=0', '--match', 'v[[:digit:]]*\\.[[:digit:]]*\\.[[:digit:]]*'
            standardOutput = stdout
        }
        def version = stdout.toString().trim()
        return !version.isEmpty() ? version - 'v' : '1.0.0-SNAPSHOT'
    }

    group = "io.sixhours"
    version = gitVersion()

    bootRepackage {
        enabled false
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }

    jar {
        manifest {
            attributes('Implementation-Title': project.name)
            attributes('Implementation-Version': project.version)
            attributes('Build-Time-ISO-8601': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"))
        }
    }

    repositories {
        jcenter()
    }

    dependencyManagement {
        dependencies {
            dependency "com.amazonaws:elasticache-java-cluster-client:$elasticache_client_version"
            dependency "commons-logging:commons-logging:$commons_logging_version"
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled true
        }
    }

    build.dependsOn jacocoTestReport
}
