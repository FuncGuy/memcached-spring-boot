buildscript {
    ext {
        spring_boot_version = '1.5.9.RELEASE'
        depedency_management_version = '1.0.4.RELEASE'
        elasticache_client_version = '1.1.1'
        commons_logging_version = '1.2'
        testcontainers_version = '1.6.0'
    }
    repositories {
        maven { url 'https://repo.spring.io/plugins-release' }
    }
    dependencies {
        classpath("io.spring.gradle:dependency-management-plugin:$depedency_management_version")
        classpath("io.spring.gradle:propdeps-plugin:0.0.9.RELEASE")
        classpath("com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3")
        classpath("org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.1")
    }
}

ext.JAVA_GRADLE = "$rootDir/gradle/java.gradle"
ext.RELEASE_GRADLE = "$rootDir/gradle/release.gradle"
ext.SONAR_GRADLE = "$rootDir/gradle/sonar.gradle"

allprojects {
    def gitVersion = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            ignoreExitValue true
            commandLine 'git', 'describe', '--tags', '--abbrev=0', '--match', 'v[[:digit:]]*\\.[[:digit:]]*\\.[[:digit:]]*'
            standardOutput = stdout
        }
        def version = stdout.toString().trim()
        return !version.isEmpty() ? version - 'v' : '1.0.0-SNAPSHOT'
    }

    group = "io.sixhours"
    version = gitVersion()
}

subprojects {
    apply from: JAVA_GRADLE
    apply from: RELEASE_GRADLE
    apply plugin: 'propdeps'
    apply plugin: 'propdeps-eclipse'
    apply plugin: 'io.spring.dependency-management'

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    jar {
        manifest {
            attributes('Implementation-Title': project.name)
            attributes('Implementation-Version': project.version)
            attributes('Build-Time-ISO-8601': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"))
        }
    }

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${spring_boot_version}"
        }
        dependencies {
            dependency "com.amazonaws:elasticache-java-cluster-client:$elasticache_client_version"
            dependency "commons-logging:commons-logging:$commons_logging_version"
            dependency "org.testcontainers:testcontainers:$testcontainers_version"
        }
    }
}
