plugins {
    id 'org.springframework.boot' version '2.2.1.RELEASE' apply false
    id 'com.jfrog.bintray' version '1.7.3'
    id 'org.sonarqube' version '2.7'
    id 'maven-publish'
    id 'java'
}

ext {
    springBootVersion = '2.2.1.RELEASE'
    springCloudVersion = 'Hoxton.RELEASE'
    elasticacheClientVersion = '1.1.1'
    commonsLoggingVersion = '1.2'
    testcontainersVersion = '1.12.3'
}

ext.JAVA_GRADLE = "$rootDir/gradle/java.gradle"
ext.PUBLISH_GRADLE = "$rootDir/gradle/publish.gradle"
ext.RELEASE_GRADLE = "$rootDir/gradle/release.gradle"
ext.SONAR_GRADLE = "$rootDir/gradle/sonar.gradle"

ext {
    projectVersion = '2.0.5-SNAPSHOT'
}

allprojects {
    def gitVersion = { ->
        def stdout = new ByteArrayOutputStream()
        exec {
            ignoreExitValue true
            commandLine 'git', 'describe', '--tags', '--abbrev=0', '--match', 'v[[:digit:]]*\\.[[:digit:]]*\\.[[:digit:]]*'
            standardOutput = stdout
        }
        def version = stdout.toString().trim()
        return !version.isEmpty() ? version - 'v' : projectVersion
    }
    def useLastTag = project.hasProperty("useLastTag") ? project.getProperty("useLastTag").toBoolean() : false

    group = "io.sixhours"
    version = useLastTag ? gitVersion() : projectVersion
}

subprojects {
    apply from: JAVA_GRADLE
    apply from: PUBLISH_GRADLE
    apply from: RELEASE_GRADLE
    apply plugin: 'io.spring.dependency-management'

    jar {
        manifest {
            attributes('Implementation-Title': project.name)
            attributes('Implementation-Version': project.version)
            attributes('Build-Time-ISO-8601': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"))
        }
    }

    repositories {
        jcenter()
    }

    dependencyManagement {
        imports {
            mavenBom org.springframework.boot.gradle.plugin.SpringBootPlugin.BOM_COORDINATES
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

//    dependencyManagement {
//        imports {
////            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
//            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
//        }
//    }
}
