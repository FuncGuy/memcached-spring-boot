apply plugin: 'java'
apply plugin: 'propdeps-idea'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    integrationTest { sourceSet ->
        ["java", "kotlin", "groovy", "scala", "resources"].each {
            if (!sourceSet.hasProperty(it)) {
                return
            }
            sourceSet."$it".srcDir "src/integration-test/$it"
        }
        compileClasspath = sourceSets.main.output + sourceSets.test.output + configurations.integrationTestCompile
        runtimeClasspath = output + compileClasspath + configurations.integrationTestRuntime
    }
}

idea {
    module {
        ["java", "kotlin", "groovy", "scala", "resources"].each {
            if (!project.sourceSets.integrationTest.hasProperty(it)) {
                return
            }
            testSourceDirs += project.sourceSets.integrationTest."$it".srcDirs
        }
    }
}

configurations {
    integrationTestCompile {
        extendsFrom testCompile, optional, provided
    }
    integrationTestRuntime {
        extendsFrom integrationTestCompile, testRuntime
    }
}

task integrationTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the integration tests.'

    maxHeapSize = '1024m'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    mustRunAfter tasks.test
}

check.dependsOn integrationTest
