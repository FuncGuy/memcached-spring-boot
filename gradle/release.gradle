apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

ext.pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/igorbolic/memcached-spring-boot.git"
    }

    developers {
        developer {
            name 'Igor Bolic'
            email 'igor.bolic@sixhours.com'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar
            artifact javadocJar

            pom.withXml {
                def root = asNode()
                root.appendNode('name', "${project.projectName}")
                root.appendNode('description', "${project.projectDesc}")
                root.appendNode('url', 'https://github.com/igorbolic/memcached-spring-boot')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = "io.sixhours:${project.name}"
        userOrg = user
        websiteUrl = 'https://github.com/igorbolic/memcached-spring-boot'
        issueTrackerUrl = 'https://github.com/igorbolic/memcached-spring-boot/issues'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/igorbolic/memcached-spring-boot'
        version {
            name = project.version
            released = new Date()
        }
    }
}
