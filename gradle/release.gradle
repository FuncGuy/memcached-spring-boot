apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'

ext.pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }

    scm {
        url "https://github.com/sixhours-team/memcached-spring-boot.git"
    }

    developers {
        developer {
            name 'Igor Bolic'
            email 'igor.bolic@sixhours.io'
        }
        developer {
            name 'Sasa Bolic'
            email 'sasa.bolic@sixhours.io'
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            artifact sourcesJar

            if (file('src/main').exists()) {
                artifact javadocJar
            }

            pom.withXml {
                def root = asNode()
                root.appendNode('name', "${project.projectName}")
                root.appendNode('description', "${project.projectDesc}")
                root.appendNode('url', 'https://github.com/sixhours-team/memcached-spring-boot')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    pkg {
        repo = 'maven'
        name = "${project.group}:${project.name}"
        userOrg = user
        websiteUrl = 'https://github.com/sixhours-team/memcached-spring-boot'
        issueTrackerUrl = 'https://github.com/sixhours-team/memcached-spring-boot/issues'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/sixhours-team/memcached-spring-boot'
        version {
            name = project.version
            released = new Date()
        }
    }
}
